#!/usr/bin/env zsh
######################################
# Set keyboard settings on X11 session
# Usage: config-xmodmap <layout_name>
######################################
set -e
autoload dot_printf

# Set keyboard rates
xset r rate 200 70

# Give some feedback
__notify() {
    dot_printf $1
    [[ -n "$DISPLAY" ]] && notify-send config-xmodmap $1
}

# Basic variables
xmodmap_dir=~/.Xmodmap.d # Base xmodmap files directory
xmodmap_layout_dir=${xmodmap_dir}/layouts-available # Available xmodmap layouts are in here
xmodmap_layout_setting=~/.xmodmap_layout

# List available layouts for xmodmap
# ----------------------------------
__xmodmap_layout_list() {
    ls $xmodmap_layout_dir
}

# Set keyboard layout via xmodmap
# -------------------------------
__xmodmap_layout_set(){
    # Restore layout from layout file for persistence
    if [[ -z "$1" ]]; then
        if [[ -a $xmodmap_layout_setting  ]]; then
            xmodmap_layout_name=$(cat $xmodmap_layout_setting)
        else
            xmodmap_layout_name=xps15
        fi
    else
        xmodmap_layout_name=$1
    fi

    # Write current layout on setting file
    echo $xmodmap_layout_name > $xmodmap_layout_setting

    # ... from which layout is retrieved
    xmodmap_layout_file="$xmodmap_layout_dir/$xmodmap_layout_name"

    if [[ ! -a "$xmodmap_layout_file" ]]; then
        __notify "No valid layout: $(basename $xmodmap_layout_file)"
        exit 1
    fi

    # Specific keycode layout is set at this point as a symlink on ~/.Xmodmap.d
    ln -sf $xmodmap_layout_file $xmodmap_dir/10-layout

    # Set keyboard mappings
    # Long story short, xmodmap does its magic using all files in ~/.Xmodmap.d
    for layout_file in ~/.Xmodmap.d/*; do
        [[ -f "$layout_file" ]] && xmodmap $layout_file
    done

    # Give feedback at the end
    __notify "layout: $(basename $xmodmap_layout_file)"
}

# The main thingy
# ---------------
case "$1" in
    list)
        __xmodmap_layout_list
        ;;
    *)
        __xmodmap_layout_set $1
        ;;
esac
